import org.gradle.util.GradleVersion

import static java.lang.Integer.parseInt

plugins {
    id 'nu.studer.plugindev' version '4.1'
    id 'com.gradle.plugin-publish' version '0.18.0'
    id 'org.nosphere.gradle.github.actions' version '1.2.0'
    id 'groovy'
}

group = 'nu.studer'
version = '6.0.2-DEV'

dependencies {
    // 3.15.1: works
    // 3.15.7: works
    // 3.16.0: does not work, javax.xml.bind.MarshalException
    // 3.16.3: does not work, javax.xml.bind.MarshalException
    api 'org.jooq:jooq-codegen:3.16.3'

    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'org.glassfish.jaxb:jaxb-core:2.3.0.1'
    implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.3'
    implementation 'javax.activation:activation:1.1.1'

    testImplementation 'com.h2database:h2:1.4.200'
    testImplementation 'org.spockframework:spock-core:2.0-groovy-2.5'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

tasks.withType(AbstractCompile).configureEach {
    options.compilerArgs <<
        "-Werror" <<
        "-Xlint:all"
}

tasks.withType(JavaCompile).configureEach {
    javaCompiler.set(javaToolchains.compilerFor { spec ->
        spec.languageVersion.set(JavaLanguageVersion.of(11))
        buildScan.value(identityPath.path + "#jvmVersion", spec.languageVersion.get().asInt() as String)
    } as Provider<? extends JavaCompiler>)
}

tasks.withType(Test).configureEach {
    maxParallelForks = 1 // there is currently only a single test class
    useJUnitPlatform()

    String testJavaRuntimeVersion = findProperty('testJavaRuntimeVersion') ?: '11'
    String testGradleVersion = findProperty('testGradleVersion') ?: GradleVersion.current().version

    javaLauncher.set(javaToolchains.launcherFor { spec ->
        spec.languageVersion.set(JavaLanguageVersion.of(testJavaRuntimeVersion))
        buildScan.value(identityPath.path + "#jvmVersion", testJavaRuntimeVersion)
    } as Provider<? extends JavaLauncher>)

    systemProperty 'testContext.gradleVersion', testGradleVersion
    buildScan.value(identityPath.path + "#gradleVersion", testGradleVersion)

    def incompatibleJavaVsGradleVersions = parseInt(testJavaRuntimeVersion) >= 16 && parseInt(testGradleVersion.substring(0, testGradleVersion.indexOf('.'))) < 7
    if (incompatibleJavaVsGradleVersions) {
        enabled = false
    }
}

gradlePlugin {
    plugins {
        pluginDevPlugin {
            id = 'nu.studer.jooq'
            implementationClass = 'nu.studer.gradle.jooq.JooqPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/etiennestuder/gradle-jooq-plugin'
    vcsUrl = 'https://github.com/etiennestuder/gradle-jooq-plugin'
    description = 'Gradle plugin that integrates jOOQ.'
    tags = ['jooq']

    plugins {
        pluginDevPlugin {
            displayName = 'gradle-jooq-plugin'
        }
    }

    mavenCoordinates {
        groupId = 'nu.studer'
        artifactId = 'gradle-jooq-plugin'
    }
}
